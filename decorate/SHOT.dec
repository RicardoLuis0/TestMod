actor PumpAmmo : Ammo{
	Inventory.MaxAmount 8
	Inventory.Icon "SHOTA0"
}

actor PumpShotgun : Weapon replaces Shotgun{
	Weapon.SlotNumber 3
	Weapon.AmmoUse1 1
	Weapon.AmmoType1 "PumpAmmo"
	Weapon.AmmoType2 "Shell"
	Weapon.AmmoGive2 16
	+Weapon.AMMO_OPTIONAL
	Inventory.PickupMessage "You've got the Pump Shotgun"
	States{
		ready:
			SHTG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			loop
		select:
			SHTG A 1 A_Raise
			loop
		deselect:
			SHTG A 1 A_Lower
			loop
		fire:
			SHTG A 0 A_JumpIfNoAmmo("reload")
			SHTG A 3
			SHTG A 0 A_FireBullets (3, 3, 12, 5, "BulletPuff")
			SHTG A 0 A_PlaySound ("SHOTFIRE",CHAN_WEAPON)
			SHTG A 7 A_GunFlash
			SHTG BC 5 A_PlaySound("SHOTPUMP")
			SHTG D 4
			SHTG CB 5
			SHTG A 3
			goto ready
		flash:
			SHTF A 4 Bright A_Light1
			SHTF B 3 Bright A_Light2
			goto lightdone
		reload:
			TNT1 A 0 A_JumpIf(CountInv("PumpAmmo")>=8||CountInv("Shell")==0,"ready")
			TNT1 A 6 A_SetInventory("Shell",CountInv("Shell")-1)
			TNT1 A 0 A_PlaySound ("weapons/sshotl")
			TNT1 A 4 A_SetInventory("PumpAmmo",CountInv("PumpAmmo")+1)
			TNT1 A 0 A_JumpIf(CountInv("PumpAmmo")==1,"pump")
			loop
		pump:
			SHTG A 5
			SHTG BC 5 A_PlaySound("SHOTPUMP")
			SHTG D 4
			SHTG CB 5
			SHTH A 5
			goto reload
		spawn:
			SHOT A -1
			stop
	}
}