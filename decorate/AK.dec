actor AKAMMO : Ammo {
	Inventory.MaxAmount 31
	Inventory.Icon "AK4IA0"
}

actor AK : Weapon {
	Weapon.SlotNumber 4
	Weapon.AmmoType1 "AKAMMO"
	Weapon.AmmoType2 "Clip"
	Weapon.AmmoUse 1
	Weapon.AmmoGive2 30
	+Weapon.AMMO_OPTIONAL
	states{
		ready:
			AK47 A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			loop
		select:
			AK47 A 1 A_Raise
			loop
		deselect:
			AK47 A 1 A_Lower
			loop
		fire:
			TNT1 A 0 {
				if(CountInv("AKAMMO")==0){
					return State("ready");
				}
				A_FireBullets(4,4,1,7,"BulletPuff");
				A_PlaySound("AKFIRE");
				A_SetPitch(pitch-(random(0,10)/5));
				A_SetAngle(angle-(random(0,15)/10));
				return State("");
			}
			AK4F A 1 BRIGHT
			AK4F B 1 BRIGHT
			AK47 B 1
			AK47 B 2 A_ReFire
			goto ready
		spawn:
			AK4I A -1
			stop
		reload:
			TNT1 A 0 A_JumpIf(CountInv("AKAMMO")==31||CountInv("Clip")==0,"ready")
			TNT1 A 0{
				if(CountInv("AKAMMO")==0){
					if(CountInv("Clip")>30){
						A_SetInventory("AKAMMO",30);
						A_SetInventory("Clip",CountInv("Clip")-30);
					}else{
						A_SetInventory("AKAMMO",CountInv("Clip"));
						A_SetInventory("Clip",0);
					}
				}else{
					if(CountInv("Clip")>31-CountInv("AKAMMO")){
						A_SetInventory("Clip",CountInv("Clip")-(31-CountInv("AKAMMO")));
						A_SetInventory("AKAMMO",31);
					}else{
						A_SetInventory("AKAMMO",CountInv("AKAMMO")+CountInv("Clip"));
						A_SetInventory("Clip",0);
					}
				}
			}
			TNT1 A 5
			TNT1 A 10 A_PlaySound("weapons/sshoto")
			AK47 A 1
			goto ready
	}
}